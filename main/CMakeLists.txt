INCLUDE_DIRECTORIES(
  ../src/gui
)

INCLUDE(${QT_USE_FILE})
ADD_DEFINITIONS(${QT_DEFINITIONS})

SET(SOURCES_RC resources/QSeriesHunter.qrc)
QT4_ADD_RESOURCES(SOURCES_RC_SRC ${SOURCES_RC} )

if (WIN32)
	ADD_EXECUTABLE(${PROJECT_NAME} WIN32 main.cpp ${SOURCES_RC_SRC} "${CMAKE_CURRENT_SOURCE_DIR}/Resources\\\\QSeriesHunter.rc")
elseif (APPLE)
	ADD_EXECUTABLE(${PROJECT_NAME} MACOSX_BUNDLE main.cpp ${SOURCES_RC_SRC})
endif (WIN32)

TARGET_LINK_LIBRARIES(${PROJECT_NAME} ${QT_LIBRARIES} src)

INSTALL(TARGETS ${PROJECT_NAME} 
        BUNDLE DESTINATION . COMPONENT Runtime
        RUNTIME DESTINATION bin COMPONENT Runtime) 

SET(DIRS ${QT_LIBRARY_DIR} ${LIBRARY_OUTPUT_PATH})

SET(CPACK_PACKAGE_VERSION_MAJOR ${QSERIESHUNTER_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${QSERIESHUNTER_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${QSERIESHUNTER_PATCH})

if (WIN32)
	SET(GENERATOR "NSIS")
	SET(PROJECT_RUNTIME "bin")
	SET(PROJECT_EXTENSION "exe")
	SET(CPACK_NSIS_MODIFY_PATH OFF)
	SET(CPACK_NSIS_DISPLAY_NAME ${PROJECT_NAME})
	SET(CPACK_PACKAGE_INSTALL_DIRECTORY ${PROJECT_NAME})
	SET(EXEC_NAME "${PROJECT_NAME}.${PROJECT_EXTENSION}")
	SET(CPACK_NSIS_INSTALLED_ICON_NAME "${EXEC_NAME}")
	SET(CPACK_NSIS_HELP_LINK "https:\\\\\\\\github.com/docdoc/QSeriesHunter")
	SET(CPACK_NSIS_URL_INFO_ABOUT "https:\\\\\\\\github.com/docdoc/QSeriesHunter")
	SET(CPACK_NSIS_MUI_ICON "${CMAKE_CURRENT_SOURCE_DIR}/Resources\\\\QSeriesHunter.ico")
	SET(CPACK_NSIS_MUI_UNIICON "${CMAKE_CURRENT_SOURCE_DIR}/Resources\\\\QSeriesHunter.ico")
	SET(CPACK_NSIS_CREATE_ICONS "CreateShortCut '\$SMPROGRAMS\\\\$STARTMENU_FOLDER\\\\${PROJECT_NAME}.lnk' '\$INSTDIR\\\\bin\\\\${PROJECT_NAME}.exe'")
	SET(CPACK_NSIS_MUI_FINISHPAGE_RUN "${EXEC_NAME}")
	LIST(APPEND DIRS "C:/Program Files/qjson/bin")
    INSTALL(CODE "include(BundleUtilities)
    fixup_bundle(\"\${CMAKE_INSTALL_PREFIX}/${PROJECT_RUNTIME}/${PROJECT_NAME}.${PROJECT_EXTENSION}\"   \"\"   \"${DIRS}\")
    " COMPONENT Runtime)
elseif (APPLE)
	SET(GENERATOR "DragNDrop")
	SET(PROJECT_RUNTIME "")
	SET(PROJECT_EXTENSION "app")
    LIST(APPEND DIRS "/usr/local/lib")  
    SET(QT_CONF_DEST_DIR "${PROJECT_NAME}.${PROJECT_EXTENSION}/Contents/Resources")
    INSTALL(CODE "
    FILE(WRITE \"\${CMAKE_INSTALL_PREFIX}/${QT_CONF_DEST_DIR}/qt.conf\" \"\")
    " COMPONENT Runtime)
	#
	SET(MACOSX_BUNDLE_ICON_FILE "${CMAKE_CURRENT_SOURCE_DIR}/Resources/QSeriesHunter.icns")
	SET_SOURCE_FILES_PROPERTIES(${MACOSX_BUNDLE_ICON_FILE} PROPERTIES MACOSX_PACKAGE_LOCATION Resources)
	SET(CPACK_BINARY_DRAGNDROP ON)
    INSTALL(CODE "include(BundleUtilities)
    fixup_bundle(\"\${CMAKE_INSTALL_PREFIX}/${PROJECT_NAME}.${PROJECT_EXTENSION}\"   \"\"   \"${DIRS}\")
    " COMPONENT Runtime)
endif (WIN32)

SET(CPACK_GENERATOR ${GENERATOR})

include(CPack)